//////////////////////////////////////////////
// Модуль вспомогательной отладки           //
// Версия: 1.0.0.1                          //
// Автор:  Лелеко Никита, Дата: 17.12.2014	//
//////////////////////////////////////////////

// Функция позволяет устанавливать значения в режиме отладки
// Параметры:
//  Ссылка  - ссылка на значение - объект в который будет записано значение
//  Значение  - значение - устанавливаемое значение
//  ПоУмоланию  - значение - значение которое будет установлено в случае если { НЕ ЗначениеЗаполнено(Значение) }
// Возвращаемое значение:
//  ссылка  - переданная ссылка
Функция УстЗн(Ссылка, Знач Значение, Знач ПоУмолчанию = НЕОПРЕДЕЛЕНО) ЭКСПОРТ
	Ссылка = ?(ЗначениеЗаполнено(Значение), Значение, ПоУмолчанию);
	Возврат Ссылка;
КонецФункции // УстановитьЗнач()

// Функция позволяет устанавливать значения в режиме отладки
// Параметры:
//  Ссылка  - ссылка на значение - объект в который будет записано значение
//  Структура  - структура - структура, содержащая устанавливаемые ссылке свойства со значениями
// Возвращаемое значение:
//  ссылка  - переданная ссылка
Функция УстСв(Ссылка, Знач Структура) ЭКСПОРТ
	Для каждого Свойство Из Структура Цикл
		Ссылка[Свойство.Ключ] = Свойство.Значение;
	КонецЦикла; 
	Возврат Ссылка;
КонецФункции // УстановитьЗнач()

// Функция позволяет возвращать значение в зависимости от условия {?(Значение,ЕслиИстина,ЕслиЛожь)}
// Параметры:
//  Значение  - условное значение - в зависимости от его состочния будет возвращено одна из вариаций
//  ЕслиИстина  - значение - возвращается если значение приводится к истинному
//  ЕслиЛожь  - значение - возвращается если значение приводится к ложному
// Возвращаемое значение:
//  значение  - одно из 2х значений {ЕслиИстина,ЕслиЛожь}
Функция ЕслиТо(Знач Значение, Знач ЕслиИстина, Знач ЕслиЛожь) ЭКСПОРТ
	Если 		(НЕ ЗначениеЗаполнено(Значение)) 	Тогда 	Возврат ЕслиЛожь; 
	ИначеЕсли 	(ТипЗнч(Значение) = Тип("Булево")) 	Тогда 	Возврат ?(Значение = ИСТИНА, ЕслиИстина, ЕслиЛожь);
	ИначеЕсли 	(ТипЗнч(Значение) = Тип("Число")) 	Тогда 	Возврат ?(Значение <> 0, ЕслиИстина, ЕслиЛожь);
	ИначеЕсли 	(ТипЗнч(Значение) = Тип("Строка")) 	Тогда 	Возврат ?(Значение <> "", ЕслиИстина, ЕслиЛожь);
	Иначе                                                   Возврат ЕслиИстина;
	КонецЕсли;	
КонецФункции

// Функция возвращает пакет запроса
// Параметры:
//  Запрос  - {строка/запрос} - либо сам запрос либо его текст
//  СтруктураПараметров  - структура - набор параметров, которые будут установлены запросу
//  OutЗапрос  - запрос - возвращает сформированный запрос
// Возвращаемое значение:
//  массив  - результат пакетного выполнения
&НаСервере
Функция ВыпЗапрос(Знач Запрос, Знач СтруктураПараметров = Неопределено, OutЗапрос = NULL) ЭКСПОРТ
	Если ТипЗнч(Запрос) <> Тип("Запрос") Тогда
		Запрос = Новый Запрос(Запрос);
	КонецЕсли;
	OutЗапрос = Запрос;
	Если ЗначениеЗаполнено(СтруктураПараметров) Тогда
	    Для Каждого Параметр из СтруктураПараметров Цикл
			Запрос.Параметры.Вставить(Параметр.Ключ, Параметр.Значение);
		КонецЦикла;
	КонецЕсли;
	Результаты = Запрос.ВыполнитьПакет();
	Возврат Результаты;
КонецФункции